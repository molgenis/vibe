# Default DisGeNET prefixes.
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX so: <http://www.sequenceontology.org/miso/current_svn/term/SO:>
PREFIX ncit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX wi: <http://purl.org/ontology/wi/core#>
PREFIX eco: <http://purl.obolibrary.org/obo/eco.owl#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX pav: <http://purl.org/pav/>
PREFIX obo: <http://purl.obolibrary.org/obo/> 

# Custom prefixes.
PREFIX umls: <http://linkedlifedata.com/resource/umls/id/> # DisGeNET
PREFIX ncbigene: <http://identifiers.org/ncbigene/> # DisGeNET
PREFIX pda: <http://rdf.disgenet.org/resource/pda/> # DisGeNET
PREFIX gda: <http://rdf.disgenet.org/resource/gda/> # DisGeNET
PREFIX hgnc: <http://identifiers.org/hgnc.symbol/> # DisGeNET
PREFIX ordo: <http://www.orpha.net/ORDO/> # DisGeNET / Orphanet
PREFIX hoom: <http://www.semanticweb.org/ontology/HOOM#> # Orphanet
PREFIX void5: <http://rdf.disgenet.org/v5.0.0/void/> # DisGeNET
PREFIX void6: <http://rdf.disgenet.org/v6.0.0/void/> # DisGeNET
PREFIX pmid: <http://identifiers.org/pubmed/> # DisGeNET

# Query.
SELECT ?disease ?diseaseName ?gene ?geneSymbol ?gdaScoreNumber ?gdaSource ?evidenceYear ?evidence
WHERE {
	{
		SELECT DISTINCT ?disease
		WHERE {
			VALUES ?hpo { <http://purl.obolibrary.org/obo/HP_0000003> }
			{
				# Diseases that are UMLS phenotypes.
				?hpo skos:exactMatch ?disease .
			}
			UNION
			{
				# Diseases found through phenotype-disease associations.
				?hpo sio:SIO_000212/sio:SIO_000628 ?disease .
			}
			UNION
			{
				# Diseases found through Orphanet (HPO - ORDO Ontological Module).
				?hpo sio:SIO_000001/skos:exactMatch ?disease .
			}
		}
	}

	?disease sio:SIO_000212 ?gda ;
	dcterms:title ?diseaseName .
	
	?gda rdf:type/rdfs:subClassOf* sio:SIO_000983 ;
	sio:SIO_000628 ?gene ;
	sio:SIO_000216 ?gdaScoreNumber ;
	sio:SIO_000253 ?gdaSource .
	OPTIONAL {
		?gda sio:SIO_000772 ?evidence .
		?evidence dcterms:issued ?evidenceYear .
	}

	?gene sio:SIO_000205 ?geneSymbol .
}
# Disable if comparing performance, enable if comparing through shasum.
ORDER BY ?hpo ?disease ?diseaseName ?gene ?geneSymbol ?gdaScoreNumber ?gdaSource ?evidenceYear ?evidence