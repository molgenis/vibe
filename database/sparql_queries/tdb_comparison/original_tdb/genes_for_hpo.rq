# Default DisGeNET prefixes.
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX so: <http://www.sequenceontology.org/miso/current_svn/term/SO:>
PREFIX ncit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX wi: <http://purl.org/ontology/wi/core#>
PREFIX eco: <http://purl.obolibrary.org/obo/eco.owl#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX pav: <http://purl.org/pav/>
PREFIX obo: <http://purl.obolibrary.org/obo/> 

# Custom prefixes.
PREFIX umls: <http://linkedlifedata.com/resource/umls/id/> # DisGeNET
PREFIX ncbigene: <http://identifiers.org/ncbigene/> # DisGeNET
PREFIX pda: <http://rdf.disgenet.org/resource/pda/> # DisGeNET
PREFIX gda: <http://rdf.disgenet.org/resource/gda/> # DisGeNET
PREFIX hgnc: <http://identifiers.org/hgnc.symbol/> # DisGeNET
PREFIX ordo: <http://www.orpha.net/ORDO/> # DisGeNET / Orphanet
PREFIX hoom: <http://www.semanticweb.org/ontology/HOOM#> # Orphanet
PREFIX void5: <http://rdf.disgenet.org/v5.0.0/void/> # DisGeNET
PREFIX void6: <http://rdf.disgenet.org/v6.0.0/void/> # DisGeNET
PREFIX pmid: <http://identifiers.org/pubmed/> # DisGeNET

# Query.
SELECT ?disease ?diseaseName ?gene ?geneSymbol ?gdaScoreNumber ?gdaSource ?evidenceYear ?evidence
WHERE {
	{
		SELECT DISTINCT ?disease
		WHERE {
			VALUES ?hpo { <http://purl.obolibrary.org/obo/HP_0000003> }
			{
				# Diseases that are actually UMLS-codes that represent a phenotype and are an exact match with an HPO.
				?disease skos:exactMatch ?hpo .
			}
			UNION
			{
				# Diseases that have a phenotype-disease association according to sources used in DisGeNET v5.0.
				?pda sio:SIO_000628 ?hpo , ?disease .

				FILTER EXISTS { ?pda rdf:type sio:SIO_000897 . }
				# While VDAs are also rdf:type sio:SIO_010056 with an sio:SIO_000628 to ?disease, they should not match when using VALUES ?hpo.
			}
			UNION
			{
				# Diseases linked linked to phenotypes according to Orphanet (HPO - ORDO Ontological Module).
				{
					SELECT ?hpo ?orphanet
					WHERE {
						?association owl:equivalentClass/owl:intersectionOf/rdf:rest*/rdf:first ?hpoItem , ?orphaItem .
						?hpoItem owl:someValuesFrom ?hpo .
						?orphaItem owl:someValuesFrom ?orphanet .

						FILTER EXISTS { ?association rdfs:subClassOf hoom:Association . }
						FILTER EXISTS { ?hpoItem owl:onProperty <http://purl.org/oban/association_has_object> . }
						FILTER EXISTS { ?orphaItem owl:onProperty <http://purl.org/oban/association_has_subject> . }
					}
				}
				?disease skos:exactMatch ?orphanet .
			}
		}
	}

	?gda sio:SIO_000628 ?disease , ?gene ;
	sio:SIO_000216 ?gdaScore ;
	sio:SIO_000253 ?gdaSource .
	OPTIONAL {
		?gda sio:SIO_000772 ?evidence .
		?evidence dcterms:issued ?evidenceYear .
	}

	?gene sio:SIO_000205 ?geneSymbol .

	?disease dcterms:title ?diseaseName .

	?gdaScore sio:SIO_000300 ?gdaScoreNumber .

	FILTER EXISTS { ?gda rdf:type/rdfs:subClassOf* sio:SIO_000983 }
	FILTER EXISTS { ?disease rdf:type ncit:C7057 ; void:inDataset <http://rdf.disgenet.org/v6.0.0/void/disease> . }
	FILTER EXISTS { ?gene rdf:type ncit:C16612 }
	FILTER EXISTS { ?geneSymbol rdf:type ncit:C43568 }
	FILTER EXISTS { ?gdaScore rdf:type ncit:C25338 }
	FILTER EXISTS { ?gdaSource rdf:type dctypes:Dataset , dcat:Distribution }
	FILTER EXISTS { ?evidence rdf:type ncit:C47902 }
}
# Disable if comparing performance, enable if comparing through shasum.
ORDER BY ?hpo ?disease ?diseaseName ?gene ?geneSymbol ?gdaScoreNumber ?gdaSource ?evidenceYear ?evidence